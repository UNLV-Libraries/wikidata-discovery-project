# ORAL HISTORIES BY SUBJECTS + People with Oral Histories
SELECT DISTINCT ?item ?inventoryNum ?itemLabel  ?itemDescription ?subject  ?subjectLabel  ?oralHistory ?describedAt
WHERE
{
 { VALUES ?instanceOf { wd:Q558929  } # oral history entities
   ?item wdt:P5008 wd:Q100202113 ;
         wdt:P31 ?instanceOf ;
         wdt:P921 ?subject .
   OPTIONAL { ?item wdt:P217 ?inventoryNum } .
   OPTIONAL { ?item wdt:P973 ?describedAt } .
 }
UNION
 {

  ?item p:P9600 ?oralHistoryAt . # person w/ oral history entities
  ?oralHistoryAt ps:P9600 wd:Q73644758 .
  OPTIONAL {?oralHistoryAt pq:P1810 ?oralHistory } .
  OPTIONAL {?oralHistoryAt pq:P217 ?inventoryNum } .
  OPTIONAL {?oralHistoryAt pq:P973 ?describedAt } .
  OPTIONAL {?item wdt:P921 ?subject } .
  FILTER (REGEX(STR(?describedAt),"^http://n2t.net/"))
 }

 SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}