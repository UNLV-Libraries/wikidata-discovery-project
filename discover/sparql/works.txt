SELECT DISTINCT ?oclcWorkId ?work ?author ?authorLabel ?viaf_id ?workLabel ?subject ?subjectLabel ?edition ?isbn_13 ?edition_title ?edition_subTitle (YEAR(?pubDate) AS ?year) 
                ?publisher ?publisherLabel ?placeOfPub ?placeOfPubLabel ?edition_image
WHERE
{
 #?work wdt:P5008 wd:Q100202113 . # CHANGE Q-NUMBER HERE TO CTRL PROJECT SCOPE
 
 ?work wdt:P31 wd:Q47461344 ; wdt:P5331 ?oclcWorkId ; wdt:P747 ?edition ; wdt:P50 ?author .
 OPTIONAL {?author wdt:P214  ?viaf_id } .
 ?work wdt:P921 ?subject .
 ?edition wdt:P577 ?pubDate . 
 ?edition wdt:P123 ?publisher ; wdt:P291 ?placeOfPub .
 ?edition wdt:P1476 ?edition_title .
 OPTIONAL {?edition wdt:P1680 ?edition_subTitle } .
 OPTIONAL {?edition wdt:P18 ?edition_image } .
 OPTIONAL {?edition wdt:P212 ?isbn_13 } .
 SERVICE wikibase:label { bd:serviceParam wikibase:language "en,fr,de" }
}ORDER BY ASC (?work) ?workLabel
