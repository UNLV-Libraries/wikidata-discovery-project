#CORPORATE BODIES--LIST ALL
SELECT ?item ?itemLabel ?itemDescription ?streetAddress ?instanceOfLabel ?inception ?dissolved ?dateOfOpening ?dateOfClosure ?website ?location ?locationLabel ?coordinates ?subject ?subjectLabel ?parentOrgLabel ?parentOrg ?owner ?ownerLabel ?ownerDescription ?collection ?inventoryNum ?describedAt
WHERE
{
 {
  ?item wdt:P5008 wd:Q100202113 ;
        wdt:P31 ?instanceOf .
  OPTIONAL {?item wdt:P1619 ?dateOfOpening } .
  OPTIONAL {?item wdt:P3999 ?dateOfClosure } .
  OPTIONAL {?item wdt:P571 ?inception } .
  OPTIONAL {?item wdt:P576 ?dissolved } .
  OPTIONAL {?item wdt:P856 ?website } .
  OPTIONAL {?item wdt:P625 ?coordinates } .
  OPTIONAL {?item wdt:P921 ?subject } .
  OPTIONAL {?item wdt:P6375 ?streetAddress } .
  OPTIONAL {?item wdt:P276 ?location } .
  OPTIONAL {?item wdt:P749 ?parentOrg } .
  OPTIONAL {?item wdt:P127 ?owner } .

  # -- A FILTER TO FILTER OUT UNWATED P31's in RESULT SET --
  FILTER(?instanceOf NOT IN (wd:Q5, wd:Q42939539, wd:Q27032363, wd:Q28640, wd:Q3331189, wd:Q12737077, wd:Q2668072, wd:Q558929, wd:Q106813818, wd:Q9388534, wd:Q27968055, wd:Q179366, wd:Q55190113, wd:Q29023906, wd:Q35760, wd:Q51404, wd:Q170584, wd:Q16970 , wd:Q11879590, wd:Q1210334, wd:Q15243209, wd:Q5773747, wd:Q1244442, wd:Q11483816, wd:Q4164871, wd:Q17279032, wd:Q4689034, wd:Q618779, wd:Q12104567   ))
 }
UNION
 {
   ?item wdt:P31 ?instanceOf .
  ?item p:P485 ?archivesAt .
  ?archivesAt ps:P485 wd:Q73644758 .
  OPTIONAL {?archivesAt pq:P1810 ?collection } .
  OPTIONAL {?archivesAt pq:P217 ?inventoryNum } .
  OPTIONAL {?archivesAt pq:P973 ?describedAt } .
  FILTER (?instanceOf NOT IN (wd:Q5, wd:Q558929, wd:Q42939539, wd:Q27032363))
 }
SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}